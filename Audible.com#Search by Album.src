# ###################################################################
#
# Mp3tag parsing for Audible.de, created by dano on 2013-03-11
#
# 2013-09-20: Updated
# 2020-03-24: Updated
# 2020-04-06: Updated to Audible.com
#
# This file should be in your sources dir. %appdata%\roaming\mp3tag\data\sources
# On Windows XP it's C:\Documents and Settings\*username*\Application Data\Mp3tag\data\sources
#
#


[Name]=Audible.com
[BasedOn]=www.audible.com
[IndexUrl]=https://www.audible.com/search?keywords=
[AlbumUrl]=https://www.audible.com
[WordSeperator]=+
[IndexFormat]=%_url%|%Album%|%Author%|%Duration%|%Year%|%Language%
[SearchBy]=$regexp(%album%,'[- ]+cd ?\d+$',,1)
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
#DebugWriteInput "C:\Users\*username*\Desktop\mp3tag.html"
#Debug "ON" "C:\Users\*username*\Desktop\mp3tagdebug.txt"



joinuntil "</html>"
regexpreplace "[\r\n]+" ""
regexpreplace ".+results" "results"
regexpreplace "<script asyn.+" ""
regexpreplace "\s\s+" " "
regexpreplace "\t+" " "
replace "\" >" "\">"
replace "> <" "><"
replace "Running Time:" ""


findinline "results"
findinline "<h3 class=\"bc-heading"

do	
	#  Url		
	findinline "href=\""
	sayuntil "\""
	say "|"
	
	# Album
	findinline ">"
	sayuntil "<"
	
	findinline "</li>"
	if "<li class=\"bc-list-item subtitle"
		say " - "
		findinline "bc-color-secondary\">"
		sayuntil "<"
		
	else
		if  "<li class=\"bc-list-item bc"
			say " - "
			findinline ">"
			sayuntil "<"
		endif
	
	endif
	say "|" 
	
	#Author
	findinline "authorLabel\">"
	findinline "href="
	findinline ">"
	sayuntil "<"
	say "|"

	
    # Duration
    findinline "runtimeLabel\">"
    findinline "bc-color-secondary\">"
    sayuntil "<"
    say "|"
	
    # Year
    findinline "Release date:"
	sayuntil "<"
	say "|"

	# Language
	findinline "languageLabel\""
	findinline "Language:"
	sayuntil "<"
	
	saynewline
	findinline "<h3 class=\"bc-heading " 1 1

while "bc-color-link" 99


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################



# Asin
findline "\"productAsin\" value=\""
findinline "\"productAsin\" value=\""
outputto "ASIN"
sayuntil "\""
gotoline 1

# Album
outputto "Album"
findline "<h1  class=\"bc-heading"
joinuntil "authorLabel"
findinline ">"
sayuntil "<"

# Subtitle of Album
regexpreplace "  +" " "
replace "<span class=\"bc-text bc-size-medium\" ></span>" ""
findinline "bc-text bc-size-medium\" " 1 1
if ">"
	movechar 1
	outputto "Subtitle"
	sayuntil "</"
else
	gotoline 1
endif

# Author
outputto "Artist"
findline "authorLabel"
moveline 1 1
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
regexpreplace ".+By:" ""
sayrest

# narratorLabel
outputto "Composer"
findline "narratorLabel"
moveline 1 1
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
regexpreplace ".+Narrated by:" ""
sayrest

# Grouping / Series
findline "seriesLabel" 1 1
unspace
if "seriesLabel"
	outputto "SERIES"
	findline "href="
	findline ">"
	findinline ">"
	sayuntil "<"
	outputto "series-part"
	findline "a>, "
	findinline "a>, Book "
	sayuntil "<"
	outputto "Albumsort"
	sayoutput "series"
	say " "
	sayoutput "series-part"
	say " - "
	sayoutput "album"
	outputto "CONTENTGROUP"
	sayoutput "series"
	say ", Book #"
	sayoutput "series-part"
else
	IfnotOutput "Subtitle"
		outputto "Albumsort"
		sayoutput "album"
	else
		outputto "Albumsort"
		sayoutput "album"
		say ": "
		sayoutput "Subtitle"
	endif
	gotoline 1
endif

# Genres
outputto "Genre"
findline "categoriesLabel"
joinuntil "</span>"
findinline "href="
findinline ">"
sayuntil "<"

# 2nd Genre
#outputto "Category"
#findinline "href="
#findinline ">"
#sayuntil "<"

# Rating
outputto "RATING WMP"
findline "<span class=\"bc-text\" "
joinuntil "</span>"
findinline ">"
sayuntil "<"
outputto "RATING WINAMP"
sayoutput "RATING WMP"

# Description
findline "Publisher's Summary</h2>"
outputto "Comment"
findline "<span class"
joinuntil "</span>"
regexpreplace "</?[^><]+>" ""
unspace
regexpreplace "  +" " "
replace "bc-color-secondary\" >" ""
sayrest

# Copyright
findline "©"
outputto "Copyright"
findinline "©"
SkipChars "1234567890, "
sayuntil "(P)"

# Year
outputto "Year"
findinline "(P)"
SayNChars 4

# Publisher
outputto "Publisher"
sayuntil "<"

# Cover
outputto "Coverurl"
findline "\"image\": \""
replace "_SL175_" "_SS500_"
replace "_SL300_" "_SS500_"
findinline "\"image\": \""
sayuntil "\""

# Set Artist = Albumartist
outputto "albumartist"
sayoutput "Artist"

# Set Audible Album URL
outputto "WWWAUDIOSOURCE"
sayoutput "CurrentUrl"

# Experimental Tag Shoehorn
# Set Series as Movement
#outputto "MOVEMENTNAME"
#sayoutput "Series"

# Set Series # as Movement #
#outputto "MOVEMENT"
#sayoutput "series-part"

# Set Discnumber
#outputto "DISCNUMBER"
#sayoutput "series-part"

# NOT WORKING
# Set SERIES = Mood as a workaround for filtering collections in PLEX
#outputto "MOOD"
#sayoutput "series"
